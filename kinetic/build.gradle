plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id "org.openapi.generator" version "6.2.1"
    id 'maven-publish'
    id 'de.undercouch.download' version '5.4.0'
}

ext {
    LIBRARY_NAME = '"Kinetic Android SDK"'
    LIBRARY_VERSION = '"1.0.0-rc.17"'
}

android {
    namespace = "org.kin.kinetic"

    compileSdk 35

    defaultConfig {
        minSdk 26
        targetSdk 34

        buildConfigField 'String', 'LIBRARY_NAME', LIBRARY_NAME
        buildConfigField 'String', 'LIBRARY_VERSION', LIBRARY_VERSION
        android.buildFeatures.buildConfig true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17  // Change from 11 to 17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

dependencies {
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'io.github.funkatronics:multimult:0.2.3'
    //implementation 'org.sol4k:sol4k:0.5.4'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.9"
    implementation files('libs/borshj-0.0.0.jar')
    implementation("com.squareup.moshi:moshi-kotlin:1.13.0")
    implementation 'androidx.activity:activity:1.10.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation "com.squareup.okhttp3:okhttp:4.9.3"

    api 'com.github.metaplex-foundation.SolanaKT:solana:786b4a9'

    implementation 'com.google.code.gson:gson:2.8.6'

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:2.1.0"
}


task downloadSwagger(type: Download) {
    src "https://raw.githubusercontent.com/kin-labs/kinetic/main/api-swagger.json"
    dest "$rootDir/api-swagger.json"
}

openApiGenerate {
    generatorName = "kotlin"
    ignoreFileOverride = "$rootDir/kinetic/.openapi.ignore".toString()
    inputSpec = "$rootDir/api-swagger.json".toString()
    outputDir = "$rootDir".toString()
    apiPackage = "org.kin.kinetic.generated.api"
    modelPackage = "org.kin.kinetic.generated.api.model"
    packageName = "org.kin.kinetic.generated"
    generateModelDocumentation = false
    generateModelTests = false
    generateApiDocumentation = false
    generateApiTests = false
    templateDir = "$rootDir/.openapi-generator/templates/kotlin-client"
    configOptions = [
            dateLibrary: "java8",
            omitGradleWrapper: "true",
            sourceFolder: "kinetic/src/main/java",
    ]
    globalProperties = [
            modelDocs: "false",
    ]
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/kotlin"
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = "org.kin"
            artifactId = "kinetic"
            version = LIBRARY_VERSION

            afterEvaluate {
                from components.release
            }
        }
    }
}
